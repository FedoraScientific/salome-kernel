//  Copyright (C) 2003  OPEN CASCADE, EADS/CCR, LIP6, CEA/DEN,
//  CEDRAT, EDF R&D, LEG, PRINCIPIA R&D, BUREAU VERITAS 
// 
//  This library is free software; you can redistribute it and/or 
//  modify it under the terms of the GNU Lesser General Public
//  License as published by the Free Software Foundation; either
//  version 2.1 of the License.
//
//  This library is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
//  Lesser General Public License for more details.
//
//  You should have received a copy of the GNU Lesser General Public
//  License along with this library; if not, write to the Free Software
//  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
//
//  See http://www.opencascade.org/SALOME/ or email : webmaster.salome@opencascade.org
//
//
//
//  File   : SALOMEDS.idl
//  Author : Yves FRICAUD
//  $Header$

/*! \mainpage
    \image html Application-About.png
*/

/*!
  \file SALOMEDS.idl This file contains a set of interfaces used for creation, managment
  and modification of the %Study
*/

#ifndef _SALOMEDS_IDL_
#define _SALOMEDS_IDL_

#include "SALOME_Exception.idl"
#include "SALOME_GenericObj.idl"

/*!
     This package contains the interfaces used for creation, managment
     and modification of the %Study
*/
module SALOMEDS
{
/*! \typedef URL
    Name of the file in which the %Study is saved.

*/
  typedef string URL;

/*! Main identifier of an object in %SALOME application
*/
  typedef string ID;

/*! While saving the data, IOR is transformed into persistent reference
*/
  typedef string PersistentReference;

/*! IOR of the study in %SALOME application
*/
  typedef string SalomeReference;

/*! List of the names of studies which are currently open in this %SALOME session.
Since %SALOME is a multi-study application, it allows to open a lot of studies
during each working session.
*/
  typedef sequence<string> ListOfOpenStudies;
/*! List of file names
*/
  typedef sequence<string> ListOfFileNames;
/*! List of modification dates of a study
*/
  typedef sequence<string> ListOfDates ;
/*! An unbounded sequence of strings
*/
  typedef sequence<string> ListOfStrings ;
/*! A byte stream which is used for binary data transfer between different components
*/
  typedef sequence<octet> TMPFile;

  // Reference to other objects is treated with function AddReference
  // and ReferencedObject
  // All other type of attributes defined in AttributeType enum are
  // treated with AddAdttribute and GetAttribute
  // The difference is made because Reference attribute don't contain
  // strings but reference to ID of other objects

  interface GenericAttribute;
  interface Study;
  interface StudyManager;
  interface StudyBuilder;
  interface SObject;
  interface SComponent;
  interface SComponentIterator;
  interface ChildIterator;
  interface Driver;
  interface AttributeStudyProperties;
  interface AttributeParameter;
  interface UseCaseIterator;
  interface UseCaseBuilder;

/*! List of attributes of %SObjects
*/
  typedef sequence<GenericAttribute> ListOfAttributes;

/*! Exception indicating that this feature hasn't been implemented in %SALOME PRO application.
*/
  exception NotImplemented {};


  //===========================================================================
 /*! \brief %Study Interface

    The purpose of the %Study is to manage the data produced by various components of %SALOME platform.
   Most of the %Study operations are handled by the StudyManager and the StudyBuilder.
   What is left in the %Study interface are elementary inquiries.
   (Incidentally, we recall that a CORBA attribute is implemented as a pair of get
      and set methods.) A %Study is explored by a set of tools, mainly iterators
    , which are described further. Nevertheless, the %Study
     interface allows the search of an object by name or by ID.
     \note
     <BR><VAR>The Path </VAR>of an object in %SALOME application is much alike a standard path of a file.
    In general it's a string of names of directories divided by a slash '/'.
     <BR><VAR>The Context</VAR> is the current directory of an object.</P>
*/

  interface Study
  {

    exception StudyInvalidContext {};
    exception StudyInvalidComponent {};
/*! Invalid directory of the %study exception
*/
    exception StudyInvalidDirectory {};
/*! Exception pointing that this name of the study has already been used.
*/
    exception StudyNameAlreadyUsed {};
    exception StudyObjectAlreadyExists {};
/*! Invalid name of the %study exception
*/
    exception StudyNameError {};
    exception StudyCommentError {};
/*! \brief The name of the %Study

   This is equivalent to the methods setName() & getName()
*/
    attribute string     Name; // equivalent to setName() & getName()
/*! \brief The ID of the %Study

   This is equivalent to the methods setID() & getID()
*/
    attribute short      StudyId;
/*! Sequence containing %SObjects
*/
    typedef sequence<SObject> ListOfSObject;
/*!
  Gets the persistent reference to the %Study.
*/
    PersistentReference  GetPersistentReference();
/*!
  Gets a transient reference to the %Study.
*/
    SalomeReference      GetTransientReference();

/*!
    Returns True if the %Study is empty
*/
    boolean IsEmpty();
/*!
    Allows to find a %SComponent by its name.
   \param aComponentName    It's a string value in the Comment Attribute of the Component,
    which is looked for, defining the data type of this Component.

<BR><VAR>See also <A href=exemple/Example1.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>
*/
    SComponent FindComponent  (in string aComponentName);
/*!
    Allows to find a %SComponent by ID of the according %SObject
*/
    SComponent FindComponentID(in ID aComponentID);
/*!
    Allows to find a %SObject by the Name Attribute of this %SObject

    \param anObjectName String parameter defining the name of the object
    \return The obtained %SObject

<BR><VAR>See also <A href=exemple/Example19.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    SObject       FindObject      (in string anObjectName);
/*!
    Allows to find a %SObject by its ID
    \param aObjectID This parameter defines the ID of the required object
    \return The obtained %SObject

*/
    SObject       FindObjectID    (in ID aObjectID);
/*!
    Allows to create a %SObject by its ID
    \param aObjectID This parameter defines the ID of the required object
    \return The created %SObject

*/
    SObject       CreateObjectID    (in ID aObjectID);
/*!
    Allows to find a %SObject by IOR of the object belonging to this %SObject.
    \param anObjectName This parameter defines the IOR of the object
    \return The obtained %SObject

*/
    SObject       FindObjectIOR   (in ID aObjectIOR);
/*!
    Finds in the study all %SObjects produced by a given %Component.
    \param anObjectName The Name Attribute of the searched %SObjects should correspond to <VAR>anObjectName</VAR>.
    \param aComponentName The name of the component, which objects are searched for.
*/
    ListOfSObject FindObjectByName(in string anObjectName, in string aComponentName);
/*!
    Allows to find a %SObject by the path to it.

    \param thePath The path to the required %SObject.
    \return The obtained %SObject.

*/
    SObject FindObjectByPath(in string thePath);
/*!
    Returns the path to the %SObject.
*/
    string  GetObjectPath(in Object theObject);

/*!
    Sets the context of the %Study.
    \param thePath String parameter defining the context of the study.

<BR><VAR>See also <A href=exemple/Example23.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    void SetContext(in string thePath);
/*!
    Gets the context of the %Study.
    
<BR><VAR>See also <A href=exemple/Example23.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    string GetContext();
/*!
   Returns a list of names of objects corresponding to the context.
   \note  If the parameter <VAR>theContext</VAR> is empty, then the current context will be used.
*/
    ListOfStrings GetObjectNames(in string theContext);
/*!
   Returns a list of names of directories and subdirectories corresponding to the context.
   \note  If the parameter <VAR>theContext</VAR> is empty, then the current context will be used.
*/
    ListOfStrings GetDirectoryNames(in string theContext);
/*!
   Returns a list of names of Files corresponding to the context.
    \note  If the parameter <VAR>theContext</VAR> is empty, then the current context will be used.
*/
    ListOfStrings GetFileNames(in string theContext);
/*!
   Returns a list of names of Components corresponding to the context.
   \note  If the parameter <VAR>theContext</VAR> is empty, then the current context will be used.
*/
    ListOfStrings GetComponentNames(in string theContext);
/*!
    Creates a new iterator of child levels of the given %SObject.
    \param aSO The given %SObject
    \return A new iterator of child levels of the given %SObject.
*/
    ChildIterator      NewChildIterator(in SObject aSO);
/*!

    Creates a new iterator of the %SComponents.

    \return A new iterator of the %SComponents.
*/
    SComponentIterator NewComponentIterator();
/*!
   Creates a new %StudyBuilder to add or modify an object in the study.

   \return A new %StudyBuilder.

<BR><VAR>See also <A href=exemple/Example20.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    StudyBuilder NewBuilder() ;
/*! \brief Labels dependency

    Updates the map with IOR attribute. It's an inner method used for optimization.
*/
    void UpdateIORLabelMap(in string anIOR, in string anEntry);

/*! \brief Getting properties of the study

   Returns the attriubte, which contains the properties of this study.

<BR><VAR>See also <A href=exemple/Example20.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    AttributeStudyProperties GetProperties();
/*!
   Determines whether the %study has been saved
*/
    attribute boolean IsSaved;
/*!
  Returns True if the %study has been modified and not saved.
*/
    boolean IsModified();
/*!
   Determines the file where the %study has been saved
*/
    attribute string  URL;

/*! \brief List of %SObjects

    Returns the list of %SObjects which refers to %anObject.
*/
    ListOfSObject FindDependances(in SObject anObject);

/*! \brief The date of the last saving of the study

    Returns the date of the last saving of study with format: "DD/MM/YYYY HH:MM"
*/
    string GetLastModificationDate();
/*! \brief The list of modification dates of the study

    Returns the list of modification dates (without creation date) with format "DD/MM/YYYY HH:MM".
      Note : the first modification begins the list.
*/
    ListOfDates GetModificationsDate();
/*! \brief Object conversion.

    Converts an object into IOR.
    \return    IOR
*/
    string ConvertObjectToIOR(in Object theObject);
/*! \brief Object conversion.

    Converts IOR into an object.
    \return    An object
*/
    Object ConvertIORToObject(in string theIOR);
/*!
    Gets a new %UseCaseBuilder.
*/
    UseCaseBuilder  GetUseCaseBuilder();

/*!
    Closes the components in the study, removes itself from the %StudyManager.
*/
    void Close();

/*!
    Enables(if isEnabled = True)/disables automatic addition of new %SObjects to the use case.
*/
    void EnableUseCaseAutoFilling(in boolean isEnabled);

/*!
    Functions for internal usage only
*/
    void AddPostponed(in string theIOR);

    void AddCreatedPostponed(in string theIOR);

    void RemovePostponed(in long theUndoLimit);

    void UndoPostponed(in long theWay);

    boolean DumpStudy(in string thePath, in string theBaseName, in boolean isPublished);

/*!
    Returns an AttributeParameter used to store common parameters for given %theSavePoint.
    \param theID identifies a common parameters set (Example: "Interface Applicative")
    \param theSavePoint is number of a set of parameters as there can be several sets
*/
    AttributeParameter GetCommonParameters(in string theID, in long theSavePoint);

/*!
    Returns an AttributeParameter used to store parameters for given %theModuleName.
    \param theID identifies a common parameters set (Example: "Interface Applicative")
    \param theModuleName is a name of the module (Example: "Geometry")
    \param theSavePoint is number of a set of parameters as there can be several sets
*/
    AttributeParameter GetModuleParameters(in string theID, in string theModuleName, in long theSavePoint);


/*!
    Returns a default Python script to restore visual parameters for given %theModuleName.
    \param theID identifies a common parameters set (Example: "Interface Applicative")
    \param theModuleName is a name of the module (Example: "Geometry")
*/
    string GetDefaultScript(in string theID, in string theModuleName);

/*!
    Private method, returns an implementation of this Study.
   \param theHostname is a hostname of the caller
   \param thePID is a process ID of the caller
   \param isLocal is set True if the Study is launched locally with the caller
*/
    long GetLocalImpl(in string theHostname, in long thePID, out boolean isLocal);

  };

  //==========================================================================
/*! \brief %Study Builder Interface

  The purpose of the Builder is to add and/or remove objects and attributes.
  A %StudyBuilder is linked to a %Study. A
  command management is provided for the undo/redo functionalities.
  \note
  <BR><VAR>The Tag</VAR> of an item in %SALOME application is a symbolic description of
  item's position in the tree-type structure of the browser. In general it has the following
  form: <TT>0:2:1:1</TT>
*/
  //==========================================================================

  interface StudyBuilder
  {
/*! \brief %LockProtection Exception

    This exception is raised while attempting to modify a locked %study.
*/
    exception LockProtection {};
/*! \brief Creation of a new %SComponent.

   Creates a new %SComponent
   \param ComponentDataType    Data type of the %SComponent which will be created.

<BR><VAR>See also <A href=exemple/Example17.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    SComponent NewComponent(in string ComponentDataType) raises(LockProtection);
/*! \brief Definition of the instance to the %SComponent

    Defines the instance to the %SComponent.
*/
    void       DefineComponentInstance (in SComponent aComponent,in Object ComponentIOR) raises(LockProtection);
        
/*! \brief Deletion of a %SComponent

  Removes a %SComponent.
*/
    void       RemoveComponent(in SComponent aComponent) raises(LockProtection);

/*! \brief Creation of a new %SObject

   Creates a new %SObject under a definite father %SObject.

   \param theFatherObject The father %SObject under which this one should be created.
   \return New %SObject

<BR><VAR>See also <A href=exemple/Example18.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/

    SObject NewObject      (in SObject theFatherObject) raises(LockProtection);

/*! \brief Creation of a new %SObject with a definite %tag

   Creates a new %SObject with a definite %tag.

   \param atag Long value corresponding to the tag of the new %SObject.
   \return New %SObject

*/
    SObject NewObjectToTag (in SObject theFatherObject, in long atag) raises(LockProtection);
/*! \brief Deletion of the %SObject

  Removes a %SObject from the %StudyBuilder.

  \param anObject The %SObject to be deleted.
*/
    void    RemoveObject   (in SObject anObject) raises(LockProtection);
/*! \brief Deletion of the %SObject with all his child objects.

  Removes the %SObject with all his child objects.

  \param anObject The %SObject to be deleted with all child objects.
*/
    void    RemoveObjectWithChildren(in SObject anObject) raises(LockProtection);

/*!
   Loads a %SComponent.

<BR><VAR>See also <A href=exemple/Example19.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    void  LoadWith (in SComponent sco, in Driver Engine) raises (SALOME::SALOME_Exception);
/*!
   Loads a %SObject.

   \param sco %SObject to be loaded.
*/
    void  Load (in SObject sco);

/*! \brief Looking for or creating an attribute assigned to the %SObject

    Allows to find or create an attribute of a specific type which is assigned to the object.
    \param anObject        The %SObject corresponding to the attribute which is looked for.
    \param aTypeOfAttribute     Type of the attribute.

  <BR><VAR>See also <A href=exemple/Example1.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>
*/

    GenericAttribute FindOrCreateAttribute(in  SObject        anObject,
	                                 in  string         aTypeOfAttribute) raises(LockProtection);

/*! \brief Looking for an attribute assigned to a %SObject

    Allows to find an attribute of a specific type which is assigned to the object.
    \param anObject        The %SObject corresponding to the attribute which is looked for.
    \param aTypeOfAttribute     Type of the attribute.
    \param anAttribute       Where the attribute is placed if it's found.
    \return True if it finds an attribute.
 */

    boolean FindAttribute(in  SObject        anObject,
				 out GenericAttribute anAttribute,
				 in  string         aTypeOfAttribute);
/*! \brief Deleting the attribute assigned to the %SObject

    Removes the attribute of a specific type which is assigned to the object.
    \param anObject        The %SObject corresponding to the attribute.
    \param aTypeOfAttribute     Type of the attribute.

<BR><VAR>See also <A href=exemple/Example17.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>
*/
    void RemoveAttribute(in  SObject        anObject,
			 in  string         aTypeOfAttribute) raises(LockProtection);
/*!
    Adds a reference between %anObject and %theReferencedObject.
    \param anObject The %SObject which will get a reference
    \param theReferencedObject The %SObject having a reference
*/

    void Addreference(in SObject anObject,
		      in SObject theReferencedObject) ;

/*!
    Removes a reference from %anObject to another object.
    \param anObject The %SObject which contains a reference
*/

    void RemoveReference(in SObject anObject) ;

/*!
   Adds a directory in the %Study.
   \param theName String parameter defining the name of the directory.

<BR><VAR>See also <A href=exemple/Example23.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    void AddDirectory(in string theName) raises(LockProtection);

/*! \brief Identification of the %SObject's substructure.

      Identification of the %SObject's substructure by GUID.


      \param anObject The %SObject which will be identified
      \param theGUID GUID has the following format "00000000-0000-0000-0000-000000000000"
*/

     void SetGUID(in SObject anObject, in string theGUID) raises(LockProtection);
/*!
Searches for a definite %SObject with a definite GUID and returns True if it finds it.

\param anObject A definite %SObject which will be identified
\param theGUID GUID has the following format "00000000-0000-0000-0000-000000000000"
*/
     boolean IsGUID(in SObject anObject, in string theGUID);

/*! \brief Creation of a new command

   Creates a new command which can contain several different actions.
   
<BR><VAR>See also <A href=exemple/Example3.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    void NewCommand(); // command management
/*! \brief Execution of the command

   Commits all actions declared within this command.

   \exception LockProtection This exception is raised, when trying to perform this command a study, which is protected for modifications.

<BR><VAR>See also <A href=exemple/Example16.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    void CommitCommand() raises(LockProtection); // command management
/*!
    Returns True if at this moment there is a command under execution.
*/
    boolean HasOpenCommand();
/*! \brief Cancelation of the command

    Cancels all actions declared within the command.
    
<BR><VAR>See also <A href=exemple/Example17.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>
*/
    void AbortCommand(); // command management
/*! \brief Undolimit

    The number of actions which can be undone
*/
    attribute long  UndoLimit;
/*! \brief Undo method

    Cancels all actions of the last command.

    \exception LockProtection This exception is raised, when trying to perform this command a study, which is protected for modifications.

<BR><VAR>See also <A href=exemple/Example16.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    void Undo() raises (LockProtection);
/*! \brief Redo method

    Redoes all actions of the last command.

\exception LockProtection This exception is raised, when trying to perform this command a study, which is protected for modifications.

 <BR><VAR>See also <A href=exemple/Example16.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    void Redo() raises (LockProtection);
/*!
    Returns True if at this moment there are any actions which can be canceled.
    
   <BR><VAR>See also <A href=exemple/Example16.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    boolean GetAvailableUndos();
/*!
    Returns True if at this moment there are any actions which can be redone.

   <BR><VAR>See also <A href=exemple/Example3.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    boolean GetAvailableRedos();
/*!
    Puts name attribute with the given string value to the given %SObject

    \param theSO Existing SObject to set name attribute.
    \param theValue The value to be set to the name attribute.
*/
    void SetName(in SObject theSO, in string theValue) raises (LockProtection);

/*!
    Puts comment attribute with the given string value to the given %SObject

    \param theSO Existing SObject to set comment attribute.
    \param theValue The value to be set to the comment attribute.
*/
    void SetComment(in SObject theSO, in string theValue) raises (LockProtection);

/*!
    Puts IOR attribute with the given string value to the given %SObject

    \param theSO Existing SObject to set IOR attribute.
    \param theValue The value to be set to the IOR attribute.
*/
    void SetIOR(in SObject theSO, in string theValue) raises (LockProtection);
  };

  //==========================================================================
/*! \brief %Study Manager interface

    The purpose of the Manager is to manipulate the %Studies. You will find in this
    interface the methods to create, open,
    close, and save a %Study. Since a %SALOME session is multi-document, you will
    also find the methods allowing to navigate
    through the collection of studies present in a session.
*/
  //==========================================================================

  interface StudyManager
  {
/*!
    Determines whether the server has already been loaded or not.
*/
    void ping();

/*! \brief Creation of a new study

     Creates a new study with a definite name.

     \param study_name String parameter defining the name of the study

<BR><VAR>See also <A href=exemple/Example17.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    Study NewStudy(in string study_name);

/*! \brief Open a study

     Reads and activates the structure of the study %Objects.
     \param aStudyUrl The path to the study
    \warning This method doesn't activate the corba objects. Only a component can do it.

<BR><VAR>See also <A href=exemple/Example1.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>
*/
    Study Open (in URL aStudyUrl) raises (SALOME::SALOME_Exception);

/*! \brief Closing the study

    Closes a study.
*/
    void  Close(in Study aStudy);
/*! \brief Saving the study in a HDF file (or files).

    Saves a study.

    \param theMultiFile If this parameter is True the study will be saved in several files.

<BR><VAR>See also <A href=exemple/Example19.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    boolean  Save(in  Study aStudy, in boolean theMultiFile);
/*! \brief Saving a study in a ASCII file (or files).

    Saves a study in an ASCII format file (or files).
    \param theMultiFile If this parameter is True the study will be saved in several files.
*/
    boolean  SaveASCII(in  Study aStudy, in boolean theMultiFile);
/*! \brief Saving the study in a specified HDF file (or files).

    Saves the study in a specified file (or files).
    \param aUrl The path to the definite file in whcih the study will be saved
    \param aStudy The study which will be saved
    \param theMultiFile If this parameter is True the study will be saved in several files.

 <BR><VAR>See also <A href=exemple/Example1.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>
*/
    boolean  SaveAs(in URL   aUrl, // if the file already exists
		in Study aStudy,
		in boolean theMultiFile); // overwrite (as option)
/*! \brief Saving the study in a specified ASCII file (or files).

    Saves the study in a specified ASCII file (or files).

    \param aUrl The path to the definite file in whcih the study will be saved
    \param aStudy The study which will be saved
    \param theMultiFile If this parameter is True the study will be saved in several files.
*/
    boolean  SaveAsASCII(in URL   aUrl, // if the file already exists
		      in Study aStudy,
		      in boolean theMultiFile); // overwrite (as option)


/*! \brief List of open studies.

Gets the list of open studies

    \return A list of open studies in the current session.
*/
    ListOfOpenStudies GetOpenStudies();

/*! \brief Getting a particular %Study picked by name

    Activates a particular %Study
    among the session collection picking it by name.
    \param aStudyName The name of the study
*/
    Study GetStudyByName  (in string aStudyName);

/*! \brief Getting a particular %Study picked by ID

    Activates a particular %Study
    among the session collection picking it by ID.
    \param aStudyID The ID of the study
*/
    Study GetStudyByID  (in short aStudyID);

    // copy/paste methods

/*!
    Returns True, if the given %SObject can be copied to the clipboard.
*/
    boolean CanCopy(in SObject theObject);
/*!
    Returns True, if the given %SObject is copied to the clipboard.
    \param theObject The %SObject which will be copied
*/
    boolean Copy(in SObject theObject);
/*!
    Returns True, if the object from the clipboard can be pasted to the given %SObject.
    \param theObject The %SObject stored in the clipboard.
*/
    boolean CanPaste(in SObject theObject);
/*!
    Returns the %SObject in which the object from the clipboard was pasted to.
    \param theObject The %SObject which will be pasted
    \exception SALOMEDS::StudyBuilder::LockProtection This exception is raised, when trying to paste
    an object into a study, which is protected for modifications.
*/
    SObject Paste(in SObject theObject) raises (SALOMEDS::StudyBuilder::LockProtection);

/*! \brief Object conversion.

    Converts an object into IOR.
    \return    IOR
*/
    string ConvertObjectToIOR(in Object theObject);
/*! \brief Object conversion.
	    
    Converts IOR into an object.
    \return    An object
*/
    Object ConvertIORToObject(in string theIOR); 

/*!
    Private method, returns an implementation of this StudyManager.
   \param theHostname is a hostname of the caller
   \param thePID is a process ID of the caller
   \param isLocal is set True if the StudyManager is launched locally with the caller
*/
    long GetLocalImpl(in string theHostname, in long thePID, out boolean isLocal); 


  };


  //==========================================================================
/*! \brief %SObject interface

   The objects in the %study are built by the %StudyBuilder. The %SObject interface
   provides methods for elementary inquiries, like getting an object %ID or its attribuites.
 \note
 
   <BR><VAR>Tag</VAR> of an item in %SALOME application is an integer value uniquely defining an item
   in the tree-type data structure.
   <BR><VAR>ID</VAR> of an item is a description of item's position in the tree-type data structure.
   ID is a list of tags and it has the following form: <TT>0:2:1:1</TT>.
*/
  //==========================================================================

  interface SObject : SALOME::GenericObj
  {
/*! Name of the %SObject
*/
    attribute string Name; // equivalent to setName() & getName()
/*! Gets an object %ID

   \return ID of the %SObject.
*/
    ID GetID();
/*!  Acquisition of the father %Component of the %SObject

  \return The father %Component of the %SObject.
*/
    SComponent GetFatherComponent();
/*! Acquisition of the father %SObject of the %SObject

   \return the father %SObject of the given %SObject.
*/
    SObject    GetFather();
/*! Gets the %tag of a %SObject

    \return the %tag of a %SObject.
*/
    short      Tag();
/*! Gets the depth of a %SObject

    \return the depth of a %SObject.
*/
    short      Depth();
/*! Looks for subobjects of a given %SObject.

    \param atag Tag of the given %SObject
    \return True if it finds a subobject of the %SObject with a definite tag as well as the required subobject.
*/

    boolean FindSubObject (in long atag, out SObject obj);
/*! Looks for attributes of a given %SObject

   \param aTypeOfAttribute String value defining the type of the required attribute of the given %SObject.
   \return True if it finds an attribute of a definite type of the given %SObject as well as the discovered attribute.

<BR><VAR>See also <A href=exemple/Example1.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>
*/
    boolean FindAttribute(out GenericAttribute anAttribute,
				  in  string         aTypeOfAttribute);
/*! Looks for a %SObject which the given %SObject refers to.

    \return The object which the given %SObject refers to as well as True if it finds
    this object.
*/
    boolean ReferencedObject(out SObject obj); // A REVOIR
/*! Gets all attributes of a given %SObject

    \return The list of all attributes of the given %SObject.

<BR><VAR>See also <A href=exemple/Example17.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    ListOfAttributes     GetAllAttributes();
/*! Gets the study of a given %SObject.

    \return The study containing the given %SObject.
*/
    Study GetStudy();

/*! Gets the CORBA object by its own IOR attribute.
    Returns nil, if can't.

    \return The CORBA object of the %SObject.
*/
    Object GetObject();

/*!
    Returns the name attribute value of this SObject.
    Returns empty string if there is no name attribute.
*/
    string GetName();

/*!
    Returns the comment attribute value of this SObject.
    Returns empty string if there is no comment attribute.
*/
    string GetComment();

/*!
    Returns the IOR attribute value of this SObject.
    Returns empty string if there is no IOR attribute.
*/
    string GetIOR();

/*!
    Private method, returns an implementation of this SObject.
   \param theHostname is a hostname of the caller
   \param thePID is a process ID of the caller
   \param isLocal is set True if the SObject is launched locally with the caller
*/
    long GetLocalImpl(in string theHostname, in long thePID, out boolean isLocal);
  };


  //==========================================================================
/*! \brief %Generic attribute interface

   %Generic attribute is a base interface for all attributes which can be assigned to the SObjects created in the study.
*/
  //==========================================================================
  interface GenericAttribute : SALOME::GenericObj
  {
/*! \brief Exception locking all changes

    This exception locks all modifications in attributes.
*/
    exception LockProtection {};
/*! \brief Method CheckLocked

   Checks whether the %Study is protected for modifications.

   \note <BR>This exception is raised only outside a transaction.
*/
    void CheckLocked() raises (LockProtection);

    string Type();

    string GetClassType(); 	
	
    SObject GetSObject();

/*!
    Private method, returns an implementation of this GenericAttribute.
   \param theHostname is a hostname of the caller
   \param thePID is a process ID of the caller
   \param isLocal is set True if the GenericAttribute is launched locally with the caller
*/
    long GetLocalImpl(in string theHostname, in long thePID, out boolean isLocal);
  };



  //==========================================================================
/*! \brief %SComponent interface

   The %SComponent interface establishes in the study a permanent assocition to the Components integrated into %SALOME platform.
   The %SComponent interface is a specialization of the %SObject interface.
   It inherits the most of its methods from the %SObject interface.
*/
  //==========================================================================
  interface SComponent : SObject
  {
/*! Gets the data type of the given %SComponent

    \return The data type of this %SComponent.
*/
    string  ComponentDataType();
/*! Gets the IOR of the given component

  \return True (if there is an instance of the given component) and its IOR.
*/
    boolean ComponentIOR (out ID theID); //returns True if there is an instance
                                         //In this case ID identifies this one
  };


  //==========================================================================
/*! \brief %SComponentIterator interface

  This interface contains the methods allowing to iterate over all components in the list.
  The search is started from the first %SComponent in the list.
*/
  //==========================================================================
  interface SComponentIterator : SALOME::GenericObj
  {
/*!
Activates the %SComponentIterator.
*/
    void Init();
/*!  Method More

   \return True if there is one more %SComponent in the list.
*/
    boolean More();
/*!
Moves the iterator to the next %SComponent in the list.
*/
    void Next();
/*!
    Returns the %SComponent corresponding to the current %SComponent found by the iterator.

 <BR><VAR>See also <A href=exemple/Example1.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

*/
    SComponent Value();
  };

  //==========================================================================
/*! \brief %ChildIterator interface

    This interface contains methods which allow to iterate over all child
    levels.
*/
  //==========================================================================
  interface ChildIterator : SALOME::GenericObj
  {
/*!

Activates the %ChildIterator.
*/
    void Init();
/*!

Activates the %ChildIterator for all child levels.

\param allLevels If this boolean parameter is True, the %ChildIterator will be activated for all child levels.
*/
    void InitEx(in boolean allLevels);
/*! Method More

   \return True if there is one more %ChildIterator in the list.
*/
    boolean More();
/*!
    Passes the iterator to the next level.
*/
    void Next();
/*!
    Returns the %SObject corresponding to the current object found by the iterator.
*/
    SObject Value();
  };

  //==========================================================================
  //==========================================================================
/*! \brief Interface of the %UseCaseIterator.

   This interface contains a set of methods used for iteration over the objects in the use case.
*/
  interface UseCaseIterator : SALOME::GenericObj
  {
/*!
Activates the %UseCaseIterator.
\param allLevels If the value of this parameter is True the Iterator is activated for all subobjects.
*/
    void Init(in boolean allLevels);
/*! Method More

    \return True if the %UseCaseIterator finds one more object.
*/
    boolean More();
/*!
    Passes the iterator to the next object.
*/
    void Next();
/*!
    Returns the %SObject corresponding to the current object found by the Iterator.
*/
    SObject Value();
  };

  //==========================================================================
  //==========================================================================
/*! \brief Interface of the %UseCaseBuilder

   Use case in the study represents a user-managed subtree, containing all or some of the objects which exist in the study.
   The %UseCaseBuilder interface contains a set of methods used for management of the use case in the study.
*/
  interface UseCaseBuilder : SALOME::GenericObj
  {
/*!
   Adds to the use case an object as a child of the current object of the use case.

   \param theObject The added %SObject.
   \return True if this %SObject has been added in the use case.
*/
    boolean Append(in SObject theObject);
/*!
   Removes an object from the use case.

   \param theObject The deleted %SObject
   \return True if this %SObject has been deleted from the use case.
*/
    boolean Remove(in SObject theObject);
/*!
   Adds a child object <VAR>theObject</VAR> to the given father <VAR>theFather</VAR> object in the use case.
*/
    boolean AppendTo(in SObject theFather, in SObject theObject);
/*!
    Inserts in the use case the object <VAR>theFirst</VAR> before the object <VAR>theNext</VAR>.
*/
    boolean InsertBefore(in SObject theFirst, in SObject theNext);
/*!
    Sets the current object of the use case.
*/
    boolean SetCurrentObject(in SObject theObject);
/*!
    Makes the root object to be the current object of the use case.
*/
    boolean SetRootCurrent();
/*!
   Returns True if the given object <VAR>theObject</VAR> of the use case has child objects.
*/
    boolean HasChildren(in SObject theObject);
/*!
   Sets the name of the use case.
*/
    boolean SetName(in string theName);
/*!
   Gets the name of the use case.
*/
    string GetName();
/*!
   Returns True if the given object <VAR>theObject</VAR> represents a use case.
*/
    boolean IsUseCase(in SObject theObject);
/*!
    Gets the current object of the use case.
*/
    SObject GetCurrentObject();
/*!
    Creates a new use case in the use case browser.
*/
    SObject AddUseCase(in string theName);
/*!
    Returns the %UseCaseIterator for the given object <VAR>theObject</VAR> in the use case.
*/
    UseCaseIterator GetUseCaseIterator(in SObject theObject);
  };
  //==========================================================================
/*! \brief %Driver interface

This class represents a common tool for all components integrated into SALOME application, that allows them to communicate with the study. It contains a set of methods which
can be called by any component and which provide the following functionality:
<ul>
    <li> publishing in the study of the objects created by a definite component
    <li> saving/loading of the data created by a definite component. These methods are called by the StudyManager when loading/saving a study containing the data created by a definite component.
    <li> transforming of the transient references into persistant references (or vice versa) of the SObjects when saving (or loading) a study
    <li> copy/paste common functionality. These methods can be called by any component in order to copy/paste its object created in the study
</ul>

*/
  //==========================================================================
  interface Driver
  {

    /*! \brief Saving the data produced by a definite component.

        This method is called by the StudyManager when saving a study.
       \param theComponent    %SComponent corresponding to this Component
       \param theURL  The path to the file in which the data will be saved.
       \param isMultiFile  If the value of this boolean parameter is True, the data will be saved in several files.
       \return A byte stream TMPFile that contains all saved data

<BR><VAR>See also <A href=exemple/Example19.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

     */


    TMPFile Save(in SComponent theComponent, in string theURL, in boolean isMultiFile);

/*! \brief Saving the data in ASCII format produced by a definite component.

        This method is called by the StudyManager when saving a study in ASCII format.
       \param theComponent    %SComponent corresponding to this Component
       \param theURL  The path to the file in which the data will be saved.
       \param isMultiFile  If the value of this boolean parameter is True, the data will be saved in several files.
       \return A byte stream TMPFile that will contain all saved data

<BR><VAR>See also <A href=exemple/Example19.html> an example </A> of this method usage in batchmode of %SALOME application.</VAR>

     */
    TMPFile SaveASCII(in SComponent theComponent, in string theURL, in boolean isMultiFile);

    /*! \brief Loading the data.

       This method is called by the StudyManager when opening a study.
       \param theComponent      %SComponent corresponding to this Component
       \param theStream   The file which contains all data saved by the component on Save method
       \param isMultiFile  If the value of this boolean parameter is True, the data will be loaded from several files

     */

    boolean Load(in SComponent theComponent, in TMPFile theStream, in string theURL, in boolean isMultiFile);

    /*! \brief Loading the data from files in ASCII format.

       This method is called by the StudyManager when opening a study.
       \param theComponent      %SComponent corresponding to this Component
       \param theStream   The file which contains all data saved by the component on Save method
       \param isMultiFile  If the value of this boolean parameter is True, the data will be loaded from several files

     */

    boolean LoadASCII(in SComponent theComponent, in TMPFile theStream, in string theURL, in boolean isMultiFile);

    /*! \brief Closing of the study

      This method Close is called by the StudyManager when closing a study.
    \param aSComponent The according %SComponent
     */

    void Close (in SComponent aSComponent);
    //void Close ( in string  aIORSComponent);

    /*! Gets the type of the data

        \return The type of data produced by the Component in the study.
     */

     string ComponentDataType();

    // Driver Transient -> persistent called for each object in study
/*!
   Transforms IOR of a given %SObject into PersistentID. It is called for each
   object in the %study.
\note <br> In %SALOME the objects which are present in an active study are identified by an IOR, when this
study is saved these references are transformed into persintent IDs.

   \param theSObject The given %SObject.
   \param IORString The IOR of the given %SObject.
   \param isMultiFile If this parameter is True the study containing the given %SObject is stored in several files.
   \param isASCII If this parameter is True the study containing the given %SObject is stored in ASCII format.

   \return The persistent ID of the given %SObject

*/
    string IORToLocalPersistentID (in SObject theSObject,
				   in string IORString,
				   in boolean isMultiFile,
				   in boolean isASCII);
/*!
  Transforms PersistentID into IOR of the object. It is called for each
   object in the %study.

   \note <br> In %SALOME the objects which are present in an saved study (file) are identified by a persistent ID, when this
study is open, these references are transformed into persintent IORs.

   \param theSObject The given %SObject.
   \param IORString The IOR of the given %SObject.
   \param isMultiFile If this parameter is True the study containing the given %SObject is stored in several files.
   \param isASCII If this parameter is True the study containing the given %SObject is stored in ASCII format.

   \return The IOR of the given %SObject

*/
    string LocalPersistentIDToIOR (in SObject theSObject,
				   in string aLocalPersistentID,
				   in boolean isMultiFile,
				   in boolean isASCII)
      raises (SALOME::SALOME_Exception);

    // Publishing in the study
/*! Publishing in the study

    \return True if the given %Component can publish a definite object with a given IOR in the %study.
    \param theIOR The IOR of a definite object
*/
    boolean CanPublishInStudy(in Object theIOR) raises (SALOME::SALOME_Exception);
/*! \brief Publishing in the study

   Publishes the given object in the %study, using the algorithm of this component.
    \param theStudy     The %study in which the object is published
    \param theSObject     If this parameter is null the object is published for the first time. Otherwise
    this parameter should contain a reference to the object published earlier
    \param theObject      The object which is published
    \param theName      The name of the published object. If this parameter is empty, the name is generated
    automatically by the component.

    \return The published %SObject.
*/
    SObject PublishInStudy(in Study theStudy, in SObject theSObject, in Object theObject, in string theName);

    // copy/paste methods

/*!
    Returns True, if the given %SObject can be copied to the clipboard.

    \param theObject The given %SObject which should be copied.
*/
    boolean CanCopy(in SObject theObject);
/*!
    Returns the object %ID and the %TMPFile of the object from the given %SObject.
*/
    TMPFile CopyFrom(in SObject theObject, out long theObjectID);
/*!
    Returns True, if the component can paste the object with given %ID of the component with name <VAR>theComponentName</VAR>.
*/
    boolean CanPaste(in string theComponentName, in long theObjectID);
/*!
    Returns the %SObject of the pasted object.
*/
    SObject PasteInto(in TMPFile theStream, in long theObjectID, in SObject theObject);

  };
};

#endif
